// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.40'
    repositories {
        maven { setUrl("https://maven.aliyun.com/repository/central/") }
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$ANDROID_PLUGIN_VERSION"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    }
}

plugins {
    id("WMRouter") apply(false)
}

allprojects {
    repositories {
        maven { setUrl("https://maven.aliyun.com/repository/central/") }
        google()
    }
    tasks.withType(JavaCompile).configureEach { task ->
        task.options.encoding = 'UTF-8'
        task.sourceCompatibility = JavaVersion.VERSION_1_8
        task.targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = '1.8'
        }
    }
    afterEvaluate {
        if (project.plugins.hasPlugin('com.android.application') ||
                project.plugins.hasPlugin('com.android.library')) {
            // 一键替换项目内所有的compilerSdkVersion  minSdk targetVersion
            android.compileSdkVersion = 30
            android.defaultConfig {
                minSdkVersion 16
                targetSdkVersion 30
            }
            android.compileOptions {
                sourceCompatibility = JavaVersion.VERSION_1_8
                targetCompatibility = JavaVersion.VERSION_1_8
            }
            print("wmrouter projectName:" + name + "  version:" + android.compileSdkVersion)
            /* android {
                 compileSdkVersion 30
             }*/
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
